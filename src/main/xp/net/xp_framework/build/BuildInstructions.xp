package net.xp_framework.build;

import io.File;
import io.Folder;

/**
 * Override build defaults via xpbuild.json:
 *
 * ```json
 * {
 *   "base"     : "core",
 *   "naming"   : {
 *     "main"     : "xp-rt-{VERSION}.xar",
 *     "test"     : "xp-test-{VERSION}.xar"
 *   },
 *   "finalize" : "XpRelease"
 * }
 * ```
 */
public class BuildInstructions {
  public static self $DEFAULT= new self();
  public static [:ChangeLogParser] $changeLogs= [
    'ChangeLog.md' : new MarkdownChangeLogParser(),
    'ChangeLog'    : new DefaultChangeLogParser()
  ];
  protected string $base= '.';
  protected [:string] $naming= [:];
  protected string $finalize= null;

  /**
   * Sets base
   */
  public void setBase(string? $base) {
    $this.base= $base;
  }

  /**
   * Returns base
   */
  public string getBase() {
    return $this.base;
  }

  /**
   * Sets naming
   */
  public void setNaming([:string]? $naming) {
    $this.naming= $naming;
  }

  /**
   * Returns naming
   */
  public [:string] getNaming() {
    return $this.naming;
  }

  /**
   * Sets finalize
   */
  public void setFinalize(string? $finalize) {
    $this.finalize= $finalize;
  }

  /**
   * Returns finalize
   */
  public string getFinalize() {
    return $this.finalize;
  }

  /**
   * Base folder
   */
  protected Folder baseOf(Folder $base) {
    return new Folder($base, $this.base);
  }

  /**
   * Returns a file reference
   */
  public File file(Folder $base, string? $name) {
    return new File($this.baseOf($base), $name);
  }

  /**
   * Returns the ChangeLog or NULL if no ChangeLog exists
   */
  public ChangeLog changeLogIn(Folder $base) {
    foreach ($name, $parser in self::$changeLogs) {
      $f= $this.file($base, $name);
      if ($f.exists()) return $parser.parse($f.getInputStream());
    }
    return null;
  }

  /**
   * Creates a string representation
   */
  public string toString() {
    return $this.getClassName() ~ '@' ~ xp::stringOf([
      base     : $this.base,
      naming   : $this.naming,
      finalize : $this.finalize
    ]);
  }
}