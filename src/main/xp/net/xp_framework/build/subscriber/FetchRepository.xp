package net.xp_framework.build.subscriber;

import peer.http.HttpConnection;
import peer.http.HttpConstants;
import io.Folder;
import io.File;
import io.FileUtil;
import io.archive.zip.ZipFile;
import io.archive.zip.ZipArchiveReader;
import io.streams.StreamTransfer;
import net.xp_framework.build.Version;
import net.xp_framework.build.Release;
import net.xp_framework.build.ChangeLog;
import net.xp_framework.build.BuildInstructions;
import util.Date;
import util.Properties;

import native standard.sprintf;
import native standard.sscanf;
import native standard.basename;
import native standard.rtrim;
import native standard.str_replace;
import native standard.strtr;
import native pcre.preg_replace;
import native pcre.preg_quote;
import native core.strlen;

/**
 * Fetches repository and extracts locally
 */
public class FetchRepository extends AbstractSubscriber {

  // The first %s token is replaced with the owner, the second with
  // the repository, and the third and last with the tag name
  const string ZIPBALL= 'https://github.com/%s/%s/zipball/%s';

  private Folder $target;

  /**
   * React directly on the trigger
   */
  protected string origin() {
    return 'trigger';
  }

  /**
   * Publishes final
   */
  protected string destination() {
    return 'build';
  }

  /**
   * Injects fetch configuration
   */
  [@inject(name = 'fetch')]
  public void useCheckout(Properties $prop) {
    $this.target= new Folder($prop.readString('storage', 'folder', 'checkout'));
    $this.target.exists() || $this.target.create(0755);
  }

  /**
   * Creates a HTTP connection. Uses a timeout of 10 seconds at github
   * is a bit slow in responding from while to while
   */
  protected HttpConnection connectionTo(string $url) {
    $conn= new HttpConnection($url);
    $conn.setConnectTimeout(10.0);
    return $conn;
  }

  /**
   * Fetches a ZIP file
   */
  protected ZipArchiveReader zipballOf(string $url, Folder $tempDir) {
    $headers= [:];
    do {
      $this.out.write('===> ', $url, ': ');
      $response= $this.connectionTo($url, $headers).get();
      switch ($response.statusCode()) {
        case HttpConstants::STATUS_OK:

          // Calculate file name
          if ($disposition= $response.header('Content-Disposition')) {
            $filename= null;
            sscanf($disposition[0], 'attachment; filename=%s', $filename);
          } else {
            $filename= basename($url);
          }

          // Download to a temporary location and store there so we will later be
          // able to reuse it - being nice to github's bandwidth here:)
          $tmp= new File($tempDir, $filename);
          if ($tmp.exists() && $tmp.size() == $response.header('Content-Length')[0]) {
            $this.out.writeLine('Reusing local copy');
          } else {
            $this.out.write('Downloading... ');
            try ($xfr= new StreamTransfer($response.getInputStream(), $tmp.getOutputStream())) {
              $xfr.transferAll();
            }
            $this.out.writeLine('Done');
          }

          return ZipFile::open($tmp.getInputStream());

        case HttpConstants::STATUS_FOUND: case HttpConstants::STATUS_SEE_OTHER:
          $this.out.writeLine('Redirect');
          $headers['Referer']= $url;
          $url= $response.header('Location')[0];
          continue;

        default:
          $this.out.writeLine('Error');
          throw new IllegalStateException('Unexpected response for ' ~ $url ~ ': ' ~ $response.toString());
      }
    } while (1);

    // Unreachable
  }

  /**
   * Handler for messages
   */
  [@handler]
  public [:var] fetch([:var] $build) {
    $version= new Version($build['version']);
    $this.out.writeLine('===> ', $version);

    $targetDir= new Folder($this.target, $build['owner'], $build['repo'], $build['tag']);
    $targetDir.exists() || $targetDir.create(0755);

    // Download zip file
    $zip= $this.zipballOf(sprintf(self::ZIPBALL, $build['owner'], $build['repo'], $build['tag']), $targetDir);

    // Extract the zip file
    $this.out.write('---> Exporting [');
    $i= 0;
    with ($iter= $zip.iterator()) {
      $base= rtrim(($iter.next() as io.archive.zip.ZipDirEntry).getName() ~ '/', '/');
      while ($iter.hasNext()) {
        $entry= $iter.next();
        $relative= str_replace($base, '', $entry.getName());
        if ($entry.isDirectory()) {
          $folder= new Folder($targetDir, $relative);
          $folder.exists() || $folder.create(0755);
        } else {
          $file= new File($targetDir, $relative);
          try ($tran= new StreamTransfer(($entry as io.archive.zip.ZipFileEntry).getInputStream(), $file.getOutputStream())) {
            $tran.transferAll();
          }
        }
        $i++ % 100 || $this.out.write('.');
      }
    }
    $zip.close();
    $this.out.writeLine(']');

    // Check for special build instructions
    $f= new File($targetDir, 'xpbuild.json');
    if ($f.exists()) {
      $instructions= webservices.rest.RestFormat::$JSON.read($f.getInputStream(), BuildInstructions::class) as BuildInstructions;
    } else {
      $instructions= BuildInstructions::$DEFAULT as BuildInstructions;
    }

    // Process changelog
    try {
      $log= $instructions.changeLogIn($targetDir);
      if ($version.isReleaseCandidate()) {
        $release= $log.getRelease(null);         // Use not yet named release
        $release.setVersion($version);
        $release.setRevision($build['tag']);
      } else {
        $release= $log.getRelease($version);
      }
    } catch (IllegalStateException $e) {
      $this.err.writeLine('Cannot read ChangeLog, using defaults ~ ', $e);
      $release= new Release();
      $release.setVersion($version);
      $release.setRevision($build['tag']);
      $release.setDate(Date::now());
    }

    // Republish for build process
    return [
      build    : $instructions,
      vendor   : $build['owner'],
      module   : $build['repo'],
      release  : $release,
      checkout : $targetDir.getURI()
    ];
  }
}