package net.xp_framework.build.api;

import native standard.implode;
import native standard.trim;
import native standard.array_values;

/**
 * The "/" resource shows information about the API
 */
[@webservice]
public class ApiInformation {
  const string VERSION = '2.0.0';

  /**
   * Shows API information using Swagger API
   *
   * @see  https://github.com/wordnik/swagger-core/wiki/API-Declaration
   */
  [@webmethod(verb = 'GET', path = '/', returns = 'application/json')]
  public var specification() {
    $apis= [];
    foreach ($class in $this.getClass().getPackage().getClasses()) {
      if (!$class.hasAnnotation('webservice')) continue;
      $webservice= $class.getAnnotation('webservice');

      // Compile routing
      $routing= [];
      foreach ($method in $class.getMethods()) {
        if (!$method.hasAnnotation('webmethod')) continue;
        $webmethod= $method.getAnnotation('webmethod');
        $path= implode('/', [trim($webservice['path'], '/'), trim($webmethod['path'], '/')]);
        $routing[$path][$webmethod['verb']]= [
          method           : $webmethod['verb'],
          nickname         : $method.getName(),
          summary          : $method.getComment(),
          notes            : '',          // TODO
          type             : 'object',    // TODO
          parameters       : [],          // TODO
          responseMessages : [],          // TODO
          errorResponses   : [],          // TODO
        ];
      }

      foreach ($path, $operations in $routing) {
        $apis[]= [
          path        : $path,
          description : $class.getComment(),
          operations  : array_values($operations)
        ];
      }
    }

    return [
      apiVersion     : self::VERSION,
      swaggerVersion : '1.2',
      basePath       : 'http://builds.planet-xp.net',
      apis           : $apis
    ];
  }

  /**
   * Shows API information using Hypertext Application Language
   *
   * @see  http://stateless.co/hal_specification.html
   */
  [@webmethod(verb = 'GET', path = '/', returns = 'application/hal+json')]
  public var welcome() {
    return [
      message : 'Welcome to the XP Framework Build API',
      version : self::VERSION,
      _links  : [
        self    : [ href : '/' ],
        search  : [ href : '/search?q={query}', templated : true ],
        vendors : [ href : '/vendors' ]
      ]
    ];
  }
}