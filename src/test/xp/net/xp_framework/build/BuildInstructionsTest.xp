package net.xp_framework.build;

class BuildInstructionsTest extends unittest.TestCase {

  /**
   * Test accessors for a given member (getX and setX).
   *
   * @param  instance
   * @param  member The member name
   * @param  value The value which to use for the roundtrip
   */
  protected void assertAccessors(lang.Generic $instance, string? $member, var? $value) throws unittest.AssertionFailedError {
    with ($class= $instance.getClass()) {
      $class.getMethod('set' ~ $member).invoke($instance, [$value]);
      $this.assertEquals($value, $class.getMethod('get' ~ $member).invoke($instance, []));
    }
  }

  [@test] void can_create() {
    new BuildInstructions();
  }

  [@test] void base_accessors() {
    $this.assertAccessors(new BuildInstructions(), 'base', '.');
  }

  [@test] void naming_accessors() {
    $this.assertAccessors(new BuildInstructions(), 'naming', [ name : 'test' ]);
  }

  [@test] void finalize_accessors() {
    $this.assertAccessors(new BuildInstructions(), 'finalize', 'test');
  }
}